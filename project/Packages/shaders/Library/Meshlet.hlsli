#pragma once

////////////////////////////////////////////////////////////////////////////////////////////
// Define Configs
////////////////////////////////////////////////////////////////////////////////////////////

#define _MAX_VERTICES            64
#define _MAX_INDICES             128
#define _AMPLIFICATION_NUMTHREAD 32

////////////////////////////////////////////////////////////////////////////////////////////
// Payload structure
////////////////////////////////////////////////////////////////////////////////////////////
struct Payload {
	uint meshletIndices[_AMPLIFICATION_NUMTHREAD];
	uint instanceIndices[_AMPLIFICATION_NUMTHREAD];
};

////////////////////////////////////////////////////////////////////////////////////////////
// Primitive structure
////////////////////////////////////////////////////////////////////////////////////////////
struct Primitive {
	bool isCull : SV_CullPrimitive;
};

////////////////////////////////////////////////////////////////////////////////////////////
// Meshlet structure
////////////////////////////////////////////////////////////////////////////////////////////
struct Meshlet {
	uint vertexOffset;
	uint triangleOffset;
	uint vertexCount;
	uint triangleCount;
};

////////////////////////////////////////////////////////////////////////////////////////////
// Bounds structure
////////////////////////////////////////////////////////////////////////////////////////////
struct Bounds {
	float3 center;
	float radius;
	float3 coneAxis;
	float3 coneApex;
	float coneCutoff;
};

////////////////////////////////////////////////////////////////////////////////////////////
// Frustum structure
////////////////////////////////////////////////////////////////////////////////////////////
struct Frustum {

	//=========================================================================================
	// public variables
	//=========================================================================================
	
	float4 planes[6];

	//=========================================================================================
	// public methods
	//=========================================================================================

	static Frustum Create(float4x4 viewproj) {

		Frustum frustum;

		float4x4 clip = transpose(viewproj);
	
		frustum.planes[0] = clip[3] + clip[0]; //!< Left
		frustum.planes[1] = clip[3] - clip[0]; //!< Right
		frustum.planes[2] = clip[3] + clip[1]; //!< Bottom
		frustum.planes[3] = clip[3] - clip[1]; //!< Top
		frustum.planes[4] = clip[3] + clip[2]; //!< Near
		frustum.planes[5] = clip[3] - clip[2]; //!< Far

		[unroll]
		for (uint i = 0; i < 6; ++i) {
			frustum.planes[i] /= length(frustum.planes[i].xyz);
		}

		return frustum;
	}
	
};

////////////////////////////////////////////////////////////////////////////////////////////
// Common methods
////////////////////////////////////////////////////////////////////////////////////////////

uint3 UnpackPrimitiveIndex(uint packed) {
	return uint3(
		(packed >>  0) & 0x3FF,
		(packed >> 10) & 0x3FF,
		(packed >> 20) & 0x3FF
	);
	//!< 10 bits for each index.
}
